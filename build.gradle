buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
}

ext {
    dataDir = "../utopia2-data"
}

group 'net.zweintopf'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'UtopiaKt'

defaultTasks 'clean', 'run'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.github.microutils:kotlin-logging:1.4.9"
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "commons-io:commons-io:2.4"
    compile("org.netpreserve.commons:webarchive-commons:1.1.8") {
        exclude group: "org.apache.hadoop"
    }
    compile "io.reactivex.rxjava2:rxjava:2.1.9"
    compile "io.reactivex.rxjava2:rxkotlin:2.2.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

detekt {
    version = "1.0.0.RC6-3"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
    }
}

task('downloadCrawls') {
    description 'Download crawls from commoncrawl.org using the aws tool'
    group 'Utopia'

    doLast {
        delete dataDir
        mkdir dataDir

        // Search for data files on http://index.commoncrawl.org/

        // https://en.wikipedia.org/wiki/Utopia
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891816370.72/wet/CC-MAIN-20180225110552-20180225130552-00000.warc.wet.gz'
        // http://www.zweintopf.net/Main/Zweintopf
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891815560.92/wet/CC-MAIN-20180224112708-20180224132708-00242.warc.wet.gz'
        // https://de.wikipedia.org/wiki/Utopia
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891814002.69/wet/CC-MAIN-20180222041853-20180222061853-00184.warc.wet.gz'

        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891816370.72/wet/CC-MAIN-20180225110552-20180225130552-00001.warc.wet.gz'
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891816370.72/wet/CC-MAIN-20180225110552-20180225130552-00002.warc.wet.gz'
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891815560.92/wet/CC-MAIN-20180224112708-20180224132708-00243.warc.wet.gz'
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891815560.92/wet/CC-MAIN-20180224112708-20180224132708-00244.warc.wet.gz'
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891814002.69/wet/CC-MAIN-20180222041853-20180222061853-00185.warc.wet.gz'
        download 's3://commoncrawl/crawl-data/CC-MAIN-2018-09/segments/1518891814002.69/wet/CC-MAIN-20180222041853-20180222061853-00186.warc.wet.gz'
    }
}

def download(url) {
    exec {
        commandLine 'aws', 's3', 'cp',
                '--no-sign-request',
                '--quiet',
                url,
                dataDir
    }
}
